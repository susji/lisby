;; NOTE! This is a direct translation of Matt Might's RSA example for
;; Scheme. All possible errors are most likely due to erroneous
;; translation. The source for Matt's original work is
;;
;;    https://matt.might.net/articles/implementation-of-rsa-public-key-cryptography-algorithm-in-scheme-dialect-of-lisp/
;;
;; I compared the result here against Racket v8.3, and they match.


;; lisby additions (odd?, gcd)
(define (odd? a) (= (% a 2) 1))

(define (gcd a b)
  (if (= b 0)
      a
    (gcd b (% a b))))

;; Mathematical routines.

; extended-gcd(a,b) = (x,y), such that a*x + b*y = gcd(a,b)
(define (extended-gcd a b)
  (if (= (% a b) 0)
      (list 0 1)
    (let ((x:y (extended-gcd b (% a b)))
	  (x (head x:y))
	  (y (head (tail x:y))))
      (list y (- x (* y (/ a b)))))))

; modulo-inverse(a,n) = b, such that a*b = 1 [mod n].
(define (modulo-inverse a n)
  (% (head (extended-gcd a n)) n))

; totient(n) = (p - 1)*(q - 1),
;  where pq is the prime factorization of n.
(define (totient p q) (* (- p 1) (- q 1)))

; square(x) = x^2
(define (square x) (* x x))

; modulo-power(base,exp,n) = base^exp [mod n]
(define (modulo-power base exp n)
  (if (= exp 0)
      1
      (if (odd? exp)
	  (% (* base (modulo-power base (- exp 1) n)) n)
	  (% (square (modulo-power base (/ exp 2) n)) n))))


;; RSA routines.

; A legal public exponent e is between
;  1 and totient(n), and gcd(e,totient(n)) = 1
(define (is-legal-public-exponent? e p q)
  (and (< 1 e)
       (and (< e (totient p q))
	    (= 1 (gcd e (totient p q))))))

; The private exponent is the inverse of the public exponent, mod n.
(define (private-exponent e p q)
  (if (is-legal-public-exponent? e p q)
      (modulo-inverse e (totient p q))
      (error "Not a legal public exponent for that modulus.")))

; An encrypted message is c = m^e [mod n].
(define (encrypt m e n)
  (if (> m n)
      (error "The modulus is too small to encrypt the message.")
      (modulo-power m e n)))

; A decrypted message is m = c^d [mod n].
(define (decrypt c d n)
  (modulo-power c d n))




;; RSA example.

(define p 41)       ; A "large" prime.
(define q 47)       ; Another "large" prime.
(define n (* p q))  ; The public modulus.

(define e 7)                        ; The public exponent.
(define d (private-exponent e p q)) ; The private exponent.

(define plaintext  42)
(define ciphertext (encrypt plaintext e n))

(define decrypted-ciphertext (decrypt ciphertext d n))

(display "The plaintext is:            " plaintext)
(display "The ciphertext is:           " ciphertext)
(display "The decrypted ciphertext is: " decrypted-ciphertext)

(if (= plaintext decrypted-ciphertext)
    (display "Result OK.")
  (error "RSA fail!"))
